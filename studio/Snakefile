from studio.core.dir_path import DIRPATH
from studio.core.edit_ROI import EditRoiUtils, ACTION
from studio.core.const import FILETYPE
from studio.core.snakemake.smk_utils import SmkUtils
from studio.core.utils.filepath_creater import join_filepath

configfile: join_filepath([DIRPATH.STUDIO_DIR, 'config.yaml'])

if config.get('type') == ACTION.EDIT_ROI:
    rule edit_ROI:
        conda:
            EditRoiUtils.conda(config)
        script:
            f"{DIRPATH.STUDIO_DIR}/core/rules/edit_ROI.py"
else:
    rule all:
        input: [join_filepath([DIRPATH.OUTPUT_DIR, x]) for x in config["last_output"]]


    for rule_name, details in config["rules"].items():
        if details["type"] in [FILETYPE.IMAGE, FILETYPE.CSV, FILETYPE.BEHAVIOR, FILETYPE.HDF5]:
            rule:
                input:
                    SmkUtils.input(details)
                output:
                    SmkUtils.output(details)
                params:
                    name = details
                script:
                    f"{DIRPATH.STUDIO_DIR}/core/rules/data.py"
        else:
            rule:
                input:
                    SmkUtils.input(details)
                output:
                    SmkUtils.output(details)
                params:
                    name = details
                conda:
                    SmkUtils.conda(details)
                script:
                    f"{DIRPATH.STUDIO_DIR}/core/rules/func.py"
